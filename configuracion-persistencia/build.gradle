buildscript {
    ext.kotlin_version = '1.3.11'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'idea'

group 'co.smartobjects'
version '0.0.1'
description = "Configuraci√≥n compartida para libreria de persistencia"

sourceCompatibility = 1.6
targetCompatibility = 1.6

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

compileJava {
    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.compilerArgs += '-parameters'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.6"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}


repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    def ormlite_version = "5.1"
    api "com.j256.ormlite:ormlite-core:$ormlite_version"
    api fileTree(include: 'ormlite-jdbc.jar', dir: 'libs')

    def junit5_jupiter = '5.3.1'
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5_jupiter"
}

test {
    useJUnitPlatform()

    filter {
        includeTestsMatching('*Pruebas')
    }

    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results.
    testLogging {
        events "passed", "skipped", "failed"
    }

    reports {
        junitXml.enabled = false
        html.enabled = false
    }
}