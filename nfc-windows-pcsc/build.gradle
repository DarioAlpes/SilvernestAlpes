buildscript {
    ext.kotlin_version = '1.3.11'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'idea'

group 'co.smartobjects'
version '0.0.1'
description = "NFC de Windows de pcsc"

sourceCompatibility = 1.6
targetCompatibility = 1.6

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

compileJava {
    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.compilerArgs += '-parameters'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.6"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(fileTree(include: '*.jar', dir: 'libs'))
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    api project(':nfc')
    api project(':persistencia')
    implementation project(':configuracion-persistencia-sqlite')

    // Para generar el UID
    implementation 'com.github.oshi:oshi-core:3.8.3'


    def junit5_jupiter = '5.3.1'
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5_jupiter"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5_jupiter"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junit5_jupiter"

    testImplementation 'org.junit.platform:junit-platform-launcher:1.3.1'

    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    testImplementation 'org.mockito:mockito-inline:2.19.0'
}

test {
    useJUnitPlatform()

    filter {
        includeTestsMatching('*Pruebas')
    }

    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results.
    testLogging {
        events "passed", "skipped", "failed"
    }

    reports {
        junitXml.enabled = false
        html.enabled = false
    }
}
kotlin {
    experimental {
        coroutines "enable"
    }
}